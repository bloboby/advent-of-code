# Day 16

```elixir
Mix.install([{:utils, path: "#{__DIR__}/utils"}, {:heap, "~> 3.0"}])
```

## Solution

```elixir
defmodule Day16 do
  def nbrs({x, y}) do
    %{
      :north => {x - 1, y},
      :south => {x + 1, y},
      :east => {x, y + 1},
      :west => {x, y - 1}
    }
  end

  def part1(grid, heap, seen) do
    {score, curr, dir} = Heap.root(heap)
    heap = Heap.pop(heap)
    val = Map.get(grid, curr)

    cond do
      val == ?E ->
        score

      val == ?# or {curr, dir} in seen ->
        part1(grid, heap, seen)

      true ->
        add_nbr = fn {nbr_dir, nbr}, acc ->
          score_ = if nbr_dir == dir, do: score + 1, else: score + 1001
          Heap.push(acc, {score_, nbr, nbr_dir})
        end

        heap = curr |> nbrs() |> Enum.reduce(heap, add_nbr)
        part1(grid, heap, MapSet.put(seen, {curr, dir}))
    end
  end

  def part2(target, grid, heap, best) do
    if Heap.empty?(heap) do
      best
    else
      {score, curr, dir, tiles} = Heap.root(heap)
      heap = Heap.pop(heap)
      val = Map.get(grid, curr)
      {prev_score, prev_tiles} = Map.get(best, {curr, dir}, {target, MapSet.new()})

      cond do
        val == ?# or score > prev_score ->
          part2(target, grid, heap, best)

        true ->
          add_nbr = fn {nbr_dir, nbr}, acc ->
            score_ = if nbr_dir == dir, do: score + 1, else: score + 1001
            Heap.push(acc, {score_, nbr, nbr_dir, MapSet.put(tiles, nbr)})
          end

          heap = curr |> nbrs() |> Enum.reduce(heap, add_nbr)
          tiles_ = if score < prev_score, do: tiles, else: MapSet.union(tiles, prev_tiles)
          best_ = Map.put(best, {curr, dir}, {score, tiles_})

          part2(target, grid, heap, best_)
      end
    end
  end
end

grid = "test16" |> Utils.read_grid() |> elem(0) |> Map.new()
start = grid |> Enum.find(fn {_, v} -> v == ?S end) |> elem(0)
heap = Heap.min() |> Heap.push({0, start, :east})
best_score = Day16.part1(grid, heap, MapSet.new())
```

```elixir
heap2 = Heap.min() |> Heap.push({0, start, :east, MapSet.new([start])})
end_tile = grid |> Enum.find(fn {_, v} -> v == ?E end) |> elem(0)

# 12 minutes
Day16.part2(best_score, grid, heap2, %{})
|> Map.take([:north, :south, :east, :west] |> Enum.map(&{end_tile, &1}))
|> Map.values()
|> Enum.map(&elem(&1, 1))
|> Enum.reduce(&MapSet.union/2)
|> Enum.count()
```
